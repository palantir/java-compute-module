buildscript {
    repositories {
        gradlePluginPortal()
        mavenCentral()
    }

    dependencies {
        classpath 'com.palantir.gradle.jdkslatest:gradle-jdks-latest:0.18.0'
        classpath 'com.palantir.jakartapackagealignment:jakarta-package-alignment:0.6.0'
        classpath 'com.palantir.baseline:gradle-baseline-java:6.25.0'
        classpath 'com.palantir.gradle.consistentversions:gradle-consistent-versions:2.27.0'
        classpath 'com.palantir.gradle.gitversion:gradle-git-version:3.2.0'
        classpath 'com.palantir.gradle.jdks:gradle-jdks:0.66.0'
        classpath 'com.palantir.javaformat:gradle-palantir-java-format:2.62.0'
        classpath 'com.palantir.suppressible-error-prone:gradle-suppressible-error-prone:2.9.0'
        classpath 'gradle.plugin.org.inferred:gradle-processors:3.7.0'
        classpath 'com.palantir.gradle.failure-reports:gradle-failure-reports:1.13.0'
        classpath 'com.palantir.gradle.externalpublish:gradle-external-publish-plugin:1.19.0'
        constraints {
            classpath('org.apache.logging.log4j:log4j-core:2.17.1'){ because 'Avoid vulnerable versions of log4j' }
        }
    }
}

apply plugin: 'com.palantir.failure-reports'
apply plugin: 'com.palantir.baseline'
apply plugin: 'com.palantir.git-version'
apply plugin: 'com.palantir.consistent-versions'
apply plugin: 'com.palantir.baseline-java-versions'
apply plugin: 'com.palantir.jdks.latest'

version gitVersion()

allprojects {

    repositories {
        mavenCentral()
    }

    apply plugin: 'java-library'
    apply plugin: 'com.palantir.java-format'
    apply plugin: 'com.palantir.baseline-class-uniqueness'
    apply plugin: 'com.palantir.baseline-null-away'
    apply plugin: 'com.palantir.external-publish-jar'
    apply plugin: 'com.palantir.jakarta-package-alignment'

    version rootProject.version
    group 'com.palantir.computemodules'

    tasks.withType(JavaCompile) {
        options.compilerArgs += ['-Werror']
    }

    tasks.check.dependsOn javadoc, checkUnusedDependencies
}

javaVersions {
    libraryTarget = 21
    runtime = 21
}
